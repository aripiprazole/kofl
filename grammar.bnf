//expression ::= literal | unary | binary | grouping ;
//
//literal ::= NUMBER | STRING | "true" | "false" ;
//grouping ::= "(" expression ")" ;
//unary ::= ("-" | "!") expression ;
//binary ::= expression operator expression ;
//operator ::= "=="
//    | "!=" | "<" | "<=" | ">" | ">="
//    | "+"  | "-"  | "*" | "/" ;

program ::= decl* EOF ;

decl ::= valDecl | varDecl | stmt ;

valDecl ::= "val" IDENTIFIER "=" expression ";" ;
varDecl ::= "var" IDENTIFIER "=" expression ";" ;

stmt ::= exprStmt | printStmt | block ;
exprStmt ::= expression ";" ;
printStmt ::= "print" expression ";" ;

block ::= "{" decl* "}" ;

expression ::= assignment | if;

logic_or ::= logic_and ( "or" logic_and )*;
logic_and ::= equality ( "and " equality )*;

assignment ::= IDENTIFIER "=" assignment
    | logic_or ;

if ::= "if" "(" expression ")" block ( "else" block ) ?

equality ::= comparison ( ( "!=" | "==" ) comparison)* ;
comparison ::= term ( ( ">" | ">=" | "<" | "<=" ) term)* ;
term ::= factor ( ( "-" | "+" ) factor)* ;
factor ::= unary ( ( "/" | "*" ) unary)* ;
unary ::= ( "!" | "-" ) unary | primary ;
primary ::= NUMBER | STRING
    | "true" | "false"
    | "(" expression ")"
    | IDENTIFIER ;
